#
#    Copyright 2013, 2014 Kai Pastor
#    
#    This file is part of OpenOrienteering.
# 
#    OpenOrienteering is free software: you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation, either version 3 of the License, or
#    (at your option) any later version.
# 
#    OpenOrienteering is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
# 
#    You should have received a copy of the GNU General Public License
#    along with OpenOrienteering.  If not, see <http://www.gnu.org/licenses/>.

TEMPLATE = aux

clipper.dir      = $$OUT_PWD/clipper
clipper.target   = $$clipper.dir/libpolyclipping.a
clipper.commands = \
  mkdir -p "$$clipper.dir" && \
  cd $$OUT_PWD/clipper && \
  cmake "$$PWD" -DCMAKE_TOOLCHAIN_FILE="$$OUT_PWD/../../toolchain.cmake" && \
  PATH="$$NDK_TOOLCHAIN_PATH/bin:${PATH}" $(MAKE) CXXFLAGS=\"$$QMAKE_CXXFLAGS\" LDFLAGS=\"$$QMAKE_LFLAGS\"

QMAKE_EXTRA_TARGETS += clipper
PRE_TARGETDEPS      += $$clipper.target
QMAKE_CLEAN         += $$clipper.dir/libpolyclipping.* $$clipper.dir/Clipper-prefix/src/Clipper-stamp/*

CLIPPER_PRI = \
  "$$LITERAL_HASH Generated by $$_PRO_FILE_" \
  "DEPENDPATH  += $$clipper.dir/Clipper-prefix/src/Clipper/cpp" \
  "INCLUDEPATH += $$clipper.dir/Clipper-prefix/src/Clipper/cpp" \
  "LIBS        += \"-L$$clipper.dir\""

write_file($$OUT_PWD/clipper.pri, CLIPPER_PRI)
