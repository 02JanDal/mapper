######################################################################
# @MAPPER_PRO_GENERATOR@
######################################################################

TEMPLATE = app
TARGET   = Mapper
CONFIG  -= debug_and_release

include($$OUT_PWD/../prerequisites.pri)
LIBS *= -lpolyclipping -lqtsingleapplication -locd
win32: LIBS *= -lproj-0
else:  LIBS *= -lproj

DEPENDPATH  += qmake
INCLUDEPATH += qmake

QT += core gui widgets printsupport network xml
win32: QT += core-private gui-private printsupport-private

# Defines. Use fancy quotation marks to be able to define strings with spaces.
CONFIG(debug, debug|release) {
	DEFINES += \"APP_VERSION='\\"Debug @Mapper_VERSION_MAJOR@.@Mapper_VERSION_MINOR@.@Mapper_VERSION_PATCH@\\"'\" \
	           MAPPER_DEVELOPMENT_BUILD \
	           \"MAPPER_DEVELOPMENT_RES_DIR='\\"../\\"'\"
	
	unix:QMAKE_POST_LINK += $$quote($(COPY_DIR) \"$$PWD/../symbol sets\" \"$$OUT_PWD/../symbol sets\")
}
else {
	DEFINES += \"APP_VERSION='\\"@Mapper_VERSION_MAJOR@.@Mapper_VERSION_MINOR@.@Mapper_VERSION_PATCH@\\"'\"
}
DEFINES += \"CLIPPER_VERSION='\\"@CLIPPER_VERSION@\\"'\"
DEFINES += \"MAPPER_HELP_NAMESPACE='\\"@Mapper_HELP_NAMESPACE@\\"'\"

# Input
HEADERS += \
  @MAPPER_PRO_HEADERS@

SOURCES += \
  main.cpp \
  @MAPPER_PRO_SOURCES@

RESOURCES += \
  ../resources.qrc

android {
  # Android package template customization
  ANDROID_PACKAGE_SOURCE_DIR = $$PWD/../android

  # Do not use qtsingleapplication
  LIBS -= -lqtsingleapplication
  
  # Network not needed
  QT -= network
  
  # Printing not needed
  QT -= printsupport
  
  # Use sensors, positioning and extra modules
  QT += sensors positioning androidextras
  
  # Add examples as resource
  RESOURCES += ../examples/examples.qrc
  
  # Remove legacy code
  DEFINES += NO_NATIVE_FILE_FORMAT
  HEADERS -= file_format_native.h
  SOURCES -= file_format_native.cpp
}
